
// Example game to demonstrate Adventure Script. Story is based on Undertale (by Toby Fox).


// Flowey is a meta-narrative character that lives outside normal game mechanics.
// He remembers us doing certain actions even after loading a previous save.
var floweyRemembers = false

// Damage is randomized between [0,atk], enemies do the same to you. Weapons give extra atk.
// There is no armor, and no def stat. Inventory has infinite space.
var hp = 20
var maxHp = 20
var gp = 0
var charaName = "CHARA"
var atk = 1
var xp = 0
var lv = 1
var weapon = nothing

stats {
    charaName hp gp maxHp atk xp lv weapon
}

inventory {
    stick
}

// We will use this to display stats in fights and save points.
event displayStats {
    "$charaName LV$lv HP=$hp/$maxHp gp=$gp ATK=$atk weapon=$weapon" ->
}

// Can't replicate all of the story, so there's no point giving the "determination" talk.
// Instead, we'll make it clear that [loading] is part of the narrative.
event youDie {
    "You're in pain. Everything goes dark. You die." ->
    "..."
    "Or do you?"
    load
}

event healthCheck {
    branch {
        conditions { hp <= 0 }
        trigger youDie
    }
}

event levelUpCheck {
    branch {
        conditions {
            (xp / 10) + 1 > lv
        }
        lv = (xp / 10) + 1
        "Your LOVE increased =)"
    }
}

item monsterCandy {
    description "Monster candy: Heals 5 hp"
    use {
        "You ate the monster candy" ->
        hp = -max(-maxHp, -hp - 5)
        consume
        branch {
            conditions {
                hp == maxHp
            }
            "Your HP was maxed out"
        }
        "You healed 5 HP"
    }
    //implicit equip and unequip
}

item stick {
    description "Stick: Good and sturdy"
    use {
        "You equipped the stick."
        equip
    }
    equip {
        replace weapon
        atk = atk + 2
    }
    unequip {
        atk = atk - 2
    }
}

item nothing {
    description "Nothing but your bare hands. Use this to replace your weapon."
    use {
        "You put away your weapon"
        equip
    }
    equip {
        replace weapon
    }
}

introduction {
    "You climbed a mountain that is famous for making people disappear. In hindsight, that might have been a bad idea."
    "You stumbled and fell into a hole. It was quite a fall..." ->
    "You wake up between flowers and moss. Somehow you're alright." ->
    // For meta-narrative reasons the game needs to know whether it is your first time playing.
    // Normally, the main menu would handle everything.
    branch {
        conditions {
            // Code injection needed, since we're doing something very unusual.
            @[ lastSave.location != introduction ]@
        }
        "Hey, there's a save. What do you do?" ->
        floweyRemembers = true // If there's a save, we've definitely met Flowey before.
        choices {
            "Continue" load
            "Reset" save
        }
    }
    "This DEFINITELY isn't an UNDERTALE ripoff." ->
    charaName = input_text("Please name you chara: ")
    choices {
        "Begin" goto ruinsEntrance
    }
}

location ruinsEntrance {
    "You woke up in this room. You can faintly see the sunlight filtering through from above." ->
    "It's impossible to climb back out. There's an exit to the east, leading deeper into the mountain." ->
    choices {
        "Explore the room" {
            "There's nothing to see except the flower bed. It's made of golden flowers. Looks deliberately planted."
        }
        "Progress" {
            "You step into the next room." ->
            goto ruinsEntranceFlowey
        }
    }
}

// This is the room where we first meet Flowey. He will recognize us even when he shouldn't.
location ruinsEntranceFlowey {
    "There's one door leading eastward, and one leading westward."
    // A one-time event when we meet Flowey. This is part of the "normal" narrative.
    story event {
        "There's a flower up ahead. It seems to have a... face?" ->
        "You can't cross a room without approaching it. What do you do?" ->
        choices {
            "Go back" {
                untrigger // He is patient. He'll wait until we're ready.
                "You decide to leave."
                goto ruinsEntrance
            }
            "Approach" {
                trigger floweyIntro
                finish // Exit the event.
            }
        }
    }
    // After our talk with Flowey, we'll find an otherwise unremarkable room.
    "The room where you first met Flowey. It is empty now."
    choices {
        "Go east" goto ruinsEntranceStairs
        "Go west" goto ruinsEntrance
    }
}

// This is when we first meet Flowey. He'll introduce us to the game mechanics, then tries to kill us.
event floweyIntro {
    "The flower starts talking ?!" ->
    "Flowey: Howdy! I'm Flowey. Flowey the flower."
    "$asciiArtFlowey" ->
    branch {
        conditions {
            !floweyRemembers
        }
        floweyRemembers = true // He will remember us the next time,
        save // ... and we want this information be preserved in the save files
        @[ lastSave = lastSave.copy(location = ruinsEntrance, storyState = emptyList()) ]@ // ... without revealing the trickery.
        "Flowey: You're new to the Underground, aren' tcha? Golly, you must be so confused."
        "Flowey: Let me show you how things work here."
        trigger floweyFirstFight
        finish  // Terminate the event.
    }
    "Flowey: Hee hee hee."
    "Flowey: Why did you make me introduce myself? It's rude to act like you don't know who I am."
    "Flowey: Someone's gotta teach you proper manners. I guess little old me will have to do." ->
    trigger floweyFirstFight
}

location ruinsEntranceStairs {
    "There are stairs on the east side, leading up to a door. There's also a door on the west side."
    "There's a you-sized orb in the middle of the room. It shines with golden light, and radiates a pleasant warmth."
    story event {
        "You see a glimpse of Toriel as she leaves the room. You can't catch up."
    }
    choices {
        "Go west" goto ruinsEntranceFlowey
        "Touch the orb" {
            "You touch the orb. The warmth spreads through you. (all injuries healed)"
            "You're filled with Determination. (savepoint reached)"
            hp = maxHp
            trigger displayStats
            save
        }
        "Go east" {
            "Your adventure finally begins." ->
            goto fightTestRoom
        }
    }
}

// Didn't want to replicate all of Undertale, so here's a room with enemies.
location fightTestRoom {
    "This is the fight test room. You will fight here."
    trigger doggoFight
    story event {
        conditions {
            after(doggoFight)
        }
        trigger froggitFight
    }
    "You're done with the fights."
    choices {
        "Go east" goto artifactRoom
        "Go west" goto ruinsEntranceStairs
    }
}

// Flowey pretends to helpful, but tries to kill you. The only way to win, is to not trust him.
event floweyFirstFight {
    "For a fleeting moment, you feel like a [heart-shaped object] trapped in a [box], [dodging bullets]." ->
    "Then you realize it's numbers all the way down. Trippy..."
    "Flowey: Your soul starts out weak, but you can become stronger by gaining LV."
    "Flowey: What does LV stand for? Why, LOVE of course!"
    "Flowey: You want some LOVE, don't you? Don't worry, I'll share some with you!"
    "Fist-sized translucent white orbs materialize in the air around Flowey. He winks."
    "Flowey: Down here LOVE is shared through little white... 'friendliness pellets'. Just touch them to absorb." ->
    "The 'friendliness pellets' are getting closer to you. They move slowly. You could dodge them with ease."
    choices {
        "Dodge!" {
            "You dodge them with ease. (they disappear after a distance behind you). Flowey looks confused." ->
            "Flowey: Hey, buddy. You missed them."
            "Flowey: Let's try again." ->
            "The 'friendliness pellets' come again, but with greater speed." ->
            choices {
                "Dodge!" {
                    "Flowey's face turns into a scowl. He is decidedly unhappy."
                    "Flowey: Is this a joke ?! Are you brain-dead? RUN. INTO. THE." ->
                    "Flowey: BULLETS!! (oopsie-face)" <- // the text "bullets" is quickly replaced
                    "Friendliness pellets! (innocent smile)" ->
                    "They come at you again." ->
                    choices {
                        "Dodge!" {
                            "Floweys voice deepens. He grins maliciously."
                            "$asciiArtFloweyEvil"
                            "Flowey: You know what's really going on here, don't you? You just wanted to see me suffer."
                            trigger torielSavesYouFromFlowey
                            finish // Jump out of choices by terminating the event.
                        }
                        "Do not" {
                            trigger floweyTrickedYou
                            finish // Jump out of choices by terminating the event.
                        }
                    }
                }
                "Do not." {
                    trigger floweyTrickedYou
                    finish // Jump out of choices by terminating the event.
                }
            }
        }
        "Do not." {
            trigger floweyTrickedYou
            finish // Jump out of choices by terminating the event.
        }
    }
}

event torielSavesYouFromFlowey {
    "Flowey: DIE!!" ->
    "The bullets encircle you. There's nowhere to run. Flowey laughs maniacally as they slowly close in..."
    "..."
    "Suddenly, the bullets disappear! Flowey looks confused. Before he can even react, a fireball hits him!"
    "Flowey's hurt. He runs away!"
    "$asciiArtToriel" ->
    "Youe're saved by a... goat person? (she calls herself Toriel)" ->
    "[Just pretend you understand what's happening. I can't be bothered to replicate all the Undertale dialogue]"
    "She gives you direction to her house, then leaves to 'prepare a surprise'." ->
    "You only have good feelings about this."
}

// You trusted Flowey. Bad decision.
event floweyTrickedYou {
    "You touch the orbs. They hurt! You've sustained damage." // Not really
    "$asciiArtFloweyEvil"
    "Flowey: You IDIOT. In this world, it's KILL OR BE KILLED!"
    "Flowey: Why would ANYONE pass up an opportunity like this?"
    "Flowey: DIE!!" ->
    "The bullets encircle you. There's nowhere to run. Flowey laughs maniacally as they slowly close in..."
    trigger youDie
}

// You fight an anthropomorphic dog monster. To win without killing, stay still for 3 turns.
story event doggoFight {
    var stayStill = 3
    var doggoHP = 30
    var stayedStill = false
    var doggoAtk = 5
    "Doggo: $doggoHP HP"
    trigger displayStats
    "You have encountered Doggo." ->
    choices {
        //FIGHT
        "Fight Doggo" {
            var dmg = random(atk)
            "You dealt $dmg damage." ->
            doggoHP = doggoHP - dmg
            branch {
                conditions {
                    doggoHP <= 0
                }
                "Doggo turns into dust."
                "You won. Gained 30 XP and 20 Gold"
                gp = gp + 20
                xp = xp + 30
                trigger levelUpCheck
                finish
            }
            stayedStill = false
            "You made a move, Doggo will see you next turn."
        }
        "Try to run" {
            stayedStill = false
            "You try to run, but Doggo is faster. Since you moved, he'll see you."
        }
        "Stay still" {
            stayedStill = true
        }

        //ACT
        "Check" {
            "Doggo. Only sees moving things"
        }
        "Play" {
            branch {
                conditions { has_item(stick) }
                "You throw the sick. Doggo fetches it. He seems happy."
                stayStill = 0
            }
            branch {
                conditions { !has_item(stick) }
                "You want play with Doggo, but can't. If only you had a stick..."
           }
        }

        //ITEM
        "Item" items_submenu

        //MERCY
        "Spare" {
            branch {
                conditions {
                    stayStill <= 0
                }
                "You won. Gained 0 XP and 10 Gold"
                finish
            }
            "The fight isn't over yet."
        }

        //monster's turn
        afterEach { //will only execute if choice returned normally
            branch {
                conditions { stayedStill }
                stayStill = stayStill - 1
                "You stayed still. Doggo couldn't find you." ->
                branch {
                    conditions { stayStill <= 0 }
                    "Doggo has calmed down. You could sneak away now."
                }
            }
            branch {
                conditions { !stayedStill }
                var dmg = random(doggoAtk)
                hp = hp - dmg
                "Received $dmg damage." ->
                trigger healthCheck
            }
            "Doggo: $doggoHP HP"
            trigger displayStats
        }
    }
}


event froggitFight {

}

var artifactGone = false
location artifactRoom {
    "This room houses a legendary artifact." ->
    branch {
        conditions { artifactGone }
        "The artifact is gone."
        choices {
            "Go back" goto fightTestRoom
        }
    }
    "Take it?" ->
    choices {
        "Take the artifact" {
            branch {
                conditions { !has_item(annoyingDog) }
                get_item annoyingDog
            }
            "You are carrying too many dogs."
        }
        "Go back" goto artifactRoom
    }
}
item annoyingDog {
    description "Little white dog. It's fast asleep."
    use {
        branch {
            conditions { here == artifactRoom }
            consume
            "You deployed the dog. Oh no!"
            "The dog absorbs the artifact!"
            artifactGone = true
        }
        branch {
            conditions { here != artifactRoom }
            "It keeps snoring"
        }
    }
}

