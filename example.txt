
// You can declare variables to be used in the story.
var lastSaveLocation = introduction
var floweyRemembers = false

// Story begins here, but you can jump here anytime. Console should clear when executing it.
introduction {
    // String literals are printed out. The player needs to press enter after each one.
    "You climbed a mountain that is famous for making people disappear. In hindsight, that might have been a bad idea."
    "You stumbled and fell into a hole. It was quite a fall..."
    "You wake up between flowers and moss. Somehow you're alright."
    // You can execute stuff conditionally by branching. They are processed in the orden they're written, but it is recommended to use mutually exlusive conditions.
    branch {
        // One block of conditions per branch. The logical AND is taken.
        conditions {
            lastSaveLocation != introduction
            lastSaveLocation != floweysWorld
        }
        // The branch is executed sequentially.
        "Hey, there's a save. What do you do?"
        // Choices can be presented anywhere. The player can chose exactly one.
        choices {
            "Continue" goto lastSaveLocation // Choice name and action. Use {block} for multiple actions.
            "Reset" {
                 lastSaveLocation = introduction // Assign variables based on player input.
                 goto ruinsEntrance // Change location (stops executing current location and begins the new one)
            }
        }
    }
    // Execution continues if branch didn't jump out of parent block.
    "This DEFINITELY isn't an UNDERTALE ripoff. And no, you can't name yourself." -> // If you place -> after a string, the player needn't press enter to continue.
    choices {
        "Begin" goto ruinsEntrance // If you had zero choices, the game would lock up. So don't do that.
    }
}

location ruinsEntrance {
    "You woke up in this room. You can faintly see the sunlight filtering through from above." ->
    "It's impossible to climb back out. There's an exit to the east, leading deeper into the mountain." ->
    // The parent block cannot have anything after the choices block. The choices block keeps executing until it terminates itself.
    choices {
        "Explore the room" {
            "There's nothing to see except the flower bed. It's made of golden flowers. Looks deliberately planted."
            // This didn't terminate the block, so the choices are presented again.
        }
        "Progress" {
            "You step into the next room"
            goto ruinsEntranceFlowey
        }
    }
}

location ruinsEntranceFlowey {
    "There's one door leading eastward, and one leading westward."
    // Events can happen in locations. This in an inline (unnamed) event.
    story event {
        "There's a flower up ahead. It seems to have a... face?" ->
        "You can't cross a room without approaching it. What do you do?"
        // Events can present choices.
        choices {
            "Go back" {
                untrigger
                goto ruinsEntrance
            }
            // After an event terminates, execution continues normally.
            "Approach" {
                trigger floweyIntro
                finish  // Terminate innermost event. (would get stuck otherwise)
            }
        } // If you get to this line, the choice will execute again.
    }
    choices {
        "Go east" goto ruinsEntranceStairs
        "Go west" goto ruinsEntrance
    }
}

// You can define named events. They can be placed in locations or triggered manually.
event floweyIntro {
    "The flower starts talking ?!" ->
    "It intruduces itself as 'Flowey the flower'"
    branch {
        // The conditions block (if exists) is always the first block.
        conditions {
            !floweyRemembers
        }
        floweyRemembers = true
        "He offers you a tutorial (you can't refuse)"
        trigger floweyFirstFight
        finish  // Terminate innermost event.
    }
    "He scolds you for making him introduce himself. Weird. Have you two met before?" ->
    "He gives you the tutorial anyway."
    // Events can trigger other events ad-infinitum.
    trigger floweyFirstFight
}

location ruinsEntranceStairs {
    "There are stairs on the east side, leading up to a door. There's also a door on the west side."
    "There's a you-sized orb in the middle of the room. It shines with golden light, and radiates a pleasant warmth."
    choices {
        "Go west" goto ruinsEntranceFlowey
        "Touch the orb" {
            "You touch the orb. The warmth spreads through you. (all injuries healed)"
            "You're filled with Determination. (savepoint reached)"
            lastSaveLocation = here
        }
        "Go east" {
            "Your adventure finally begins." ->
            "What a shame I didn't implement it." ->
            "So, ...uuuh... YOU WIN!"
            end //Terminate the story
        }
    }
}

location floweysWorld {
    "You shouldn't have come here."
    trigger youDie
}

event floweyFirstFight {
    "Flowey: Your soul starts out weak, but you can become stronger by gaining LV."
    "Flowey: What does LV stand for? Why, LOVE of course!"
    "Flowey: You would want some LOVE, don't you? Don't worry, I'll share some with you!"
    "Fist-sized translucent white orbs materialize in the air around flowey. He winks at you cutely."
    "Flowey: Down here LOVE is shared through little white... 'friendliness pellets'. Just touch them to absorb." ->
    "The 'friendliness pellets' are getting closer to you. They move slowly. You could dodge them with ease."
    choices {
        "Dodge!" {
            "You dodge them with ease. (they disappear after a distance behind you). Flowey looks confused." ->
            "Flowey: Hey, buddy. You missed them."
            "Flowey: Let's try again." ->
            "The 'friendliness pellets' come again, but with greater speed."
            // Choices can be presented while handling choices.
            choices {
                "Dodge!" {
                    "Flowey's face turns into a scowl. He is decidedly unhappy."
                    "Flowey: Is this a joke ?! Are you brain-dead? RUN. INTO. THE." ->
                    "Flowey: BULLETS!! (oopsie-face)" <- // The <- makes this a vanishing line.
                    "Friendliness pellets! (innocent smile)" ->
                    "They come at you again."
                    choices {
                        "Dodge!" {
                            "Floweys voice deepens. He grins maliciously."
                            "Flowey: You know what's really going on here, don't you? You just wanted to see me suffer."
                            trigger torielSavesYouFromFlowey
                            finish // Jump out of choices by terminating the event.
                        }
                        "Do not" {
                            trigger floweyTrickedYou
                            finish // Jump out of choices by terminating the event.
                        }
                    } // If you get to this line, the choice will execute again.
                }
                "Do not." {
                    trigger floweyTrickedYou
                    finish // Jump out of choices by terminating the event.
                }
            } // If you get to this line, the choice will execute again.
        }
        "Do not." {
            trigger floweyTrickedYou
            finish // Jump out of choices by terminating the event.
        }
    } // If you get to this line, the choice will execute again.
}

event torielSavesYouFromFlowey {
    "Flowey: DIE!!" ->
    "The bullets encircle you. There's nowhere to run. Flowey laughs maniacally as they slowly close in..."
    "Suddenly, the bullets disappear! Flowey looks confused. Before he can even react, a fireball hits him!"
    "Flowey's hurt. He runs away!"
}

event floweyTrickedYou {
    "You touch the orbs. They hurt! You've sustained damage."
    "Flowey: You IDIOT. In this world, it's KILL OR BE KILLED!"
    "Flowey: Why would ANYONE pass up an opportunity like this?"
    "Flowey: DIE!!" ->
    "The bullets encircle you. There's nowhere to run. Flowey laughs maniacally as they slowly close in..."
    trigger youDie
}

event youDie {
    "You're in pain. Everything goes dark. You die."
    "..."
    "Or do you?"
    goto lastSaveLocation
}

